# Ahmet Emre KILIÃ‡ 2380616
    .pos 0 # setup code
    irmovq stack, %rsp
    call main
    halt

# the array to reverse
.pos 0x100
ele0:
    .quad 0x0000a
    .quad ele1
ele1:
    .quad 0x000b0
    .quad ele2
ele2:
    .quad 0x00c00
    .quad ele3
ele3:
    .quad 0x0d000
    .quad ele4
ele4:
    .quad 0xe0000
    .quad 0

main:
    irmovq ele0, %rdi
    call rev
    ret

# Part A
rev:
    # head = %rdi
    irmovq $0, %rax         # prev initialization
loop:
    andq    %rdi,%rdi       # check head if 0
    je return
    mrmovq  8(%rdi), %r10   # next = head->next
    rmmovq  %rax, 8(%rdi)   # head->next = prev
    rrmovq  %rdi, %rax      # prev = head
    rrmovq  %r10, %rdi      # head = next
    jmp loop
return:
    ret

# stack grows upwards, so leave some space
.pos 0x300
stack:
